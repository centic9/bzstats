apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

java {
	toolchain {
		sourceCompatibility = JavaVersion.VERSION_17
	}
}

application {
	mainClass = "POIBugzillaStats"
	version = '1.0'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Apache POI Bugzilla Statistics', 'Implementation-Version': archiveVersion
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.dstadler:commons-dost:1.4.3'

    implementation 'org.apache.commons:commons-lang3:3.19.0'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.20.0'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
}

wrapper {
    gradleVersion = '8.14.3'
}

test {
    systemProperties = System.properties as Map<String, ?>

	// show standard out and standard error of the test JVM(s) on the console
	testLogging.showStandardStreams = true
}

jacoco {
    toolVersion = '0.8.14'
}

/*task createZip(type: Zip) {
    from '.'
    excludes = ['bin', 'build', '.gradle', '20*', 'Moves.txt', 'Splits.json', '.classpath', '.svn']
}*/

tasks.register('refreshStats', JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	mainClass = 'POIBugzillaStats'
	environment("BUGZILLA_API_KEY", System.getenv('BUGZILLA_API_KEY'))
}

tasks.register('createChart', Exec) {
	final String osName = System.getProperty("os.name").toLowerCase()
	if (osName.contains("windows")) {
		executable 'C:/cygwin/bin/bash.exe'
		args '-c', '/bin/R --no-save -f chart.R'
	} else {
		executable 'R'
		args '--no-save', '-f', 'chart.R'
	}
}

createChart.mustRunAfter refreshStats

tasks.register('refresh') {
	dependsOn classes, check, refreshStats, createChart
}
